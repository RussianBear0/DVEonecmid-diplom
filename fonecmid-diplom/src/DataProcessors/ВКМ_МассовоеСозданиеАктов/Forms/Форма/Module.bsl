
&НаСервере
Функция ЗаполнитьДокументыРеализацииНаСервере() 
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор,
	"Обработки.ВКМ_МассовоеСозданиеАктов.ПровестиМассивДокументовСервер",Объект.Период);	
	Возврат ДлительнаяОперация;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьДокументыРеализации(Команда) 
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина; 
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	ОповещениеОПрогрессе = Новый ОписаниеОповещения("ОповещениеОПрогрессе",ЭтотОбъект);
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессе;
	
	ДлительнаяОперация = ЗаполнитьДокументыРеализацииНаСервере(); 
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении",ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация,ОповещениеОЗавершении,ПараметрыОжидания);	

КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОПрогрессе(Результат, ДополнительныеПараметры) Экспорт

	Если Результат.Прогресс <> Неопределено Тогда
		Состояние("Операция выполняется, ожидайте", Результат.Прогресс.Процент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт

	Если Результат.Статус = "Выполнено" Тогда 
		ЗаполнитьТЧРеализаций(Результат.АдресРезультата);
		Сообщить("Обработка выполненна успешно"); 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура  ЗаполнитьТЧРеализаций(АдресРезультата) 
	ПолученныйМассив = Новый Массив;
	ПолученныйМассив = ПолучитьИзВременногоХранилища(АдресРезультата);
	Для Каждого ЭлементПолученногоМассива из ПолученныйМассив Цикл
		СтрокаТЧ = Объект.СозданныеДокументы.Добавить();
		СтрокаТЧ.Договор = ЭлементПолученногоМассива.Договор;
		СтрокаТЧ.Документ = ЭлементПолученногоМассива.Ссылка;
		СтрокаТЧ.Контрагент = ЭлементПолученногоМассива.Контрагент;  
	КонецЦикла; 
КонецПроцедуры	
