Функция ОбработатьВходящийЗапрос(Запрос) Экспорт
	
	ДанныеЗапроса = ДанныеЗапроса(Запрос);
	
	Если ДанныеЗапроса.Свойство ("message") Тогда
		Возврат ОбработатьСообщение(ДанныеЗапроса.message);
	КонецЕсли;
	
	Возврат ВКМ_ОбщегоНазначенияHTTP.ПростойУспешныйОтвет();
КонецФункции

Функция ДанныеЗапроса(Запрос)
	
	ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
	Возврат ВКМ_ОбщегоНазначенияHTTP.ОбъектJSON(ТелоЗапроса);

КонецФункции

Функция ОбработатьСообщение(ДанныеСообщения)
	
	Если ДанныеСообщения.Свойство("from") Тогда
		ЗафиксироватьОтправителя(ДанныеСообщения.from);
	КонецЕсли;
	
	ИдентификаторЧата = ДанныеСообщения.chat.id;
	
	Если ДанныеСообщения.Свойство("text") Тогда
		ТекстСообщения = ДанныеСообщения.text;
	Иначе
		ТекстСообщения = "Пустое сообщение =(";
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура;
	ДанныеОтвета.Вставить("method","sendMessage");
	ДанныеОтвета.Вставить("chat_id",ИдентификаторЧата);
	ДанныеОтвета.Вставить("text",ТекстСообщения);
	
	Возврат ВКМ_ОбщегоНазначенияHTTP.ОтветJSON(ДанныеОтвета);
	
КонецФункции

Процедура ЗафиксироватьОтправителя(ДанныеОтправителя)
	
	Идентификатор = ДанныеОтправителя.id;
	
	УчетнаяЗапись = Справочники.ВКМ_УчётныеЗаписиТелеграм.НайтиПоКоду(Идентификатор);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиИмениПользователя = Новый Массив;
	
	Если ДанныеОтправителя.Свойство("first_name") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.first_name);
		
	КонецЕсли;
	
	Если ДанныеОтправителя.Свойство("last_name") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.last_name);
		
	КонецЕсли;
	
	Если ДанныеОтправителя.Свойство("username") Тогда
		ЧастиИмениПользователя.Добавить(ДанныеОтправителя.username);
		
	КонецЕсли;
	
	ИмяПользователя = СтрСоединить(ЧастиИмениПользователя, " ");
	
	СправочникОбъект = Справочники.ВКМ_УчётныеЗаписиТелеграм.СоздатьЭлемент();
	СправочникОбъект.Наименование = ИмяПользователя;
	СправочникОбъект.Код = Идентификатор;
	СправочникОбъект.Записать();
	
	
КонецПроцедуры

Процедура ОтправитьСообщение() Экспорт
	
	//Процедура Отправляет сообщения в Телеграм из справочника
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка,
		|	ВКМ_УведомленияТелеграмБоту.Текст КАК ТекстСообщения,
		|	ВКМ_ИдентификаторГруппыДляОповещения.Значение КАК Chat_id,
		|	ВКМ_ТокенУправленияТелеграмБотом.Значение КАК TokenTG
		|ИЗ
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту,
		|	Константа.ВКМ_ТокенУправленияТелеграмБотом КАК ВКМ_ТокенУправленияТелеграмБотом,
		|	Константа.ВКМ_ИдентификаторГруппыДляОповещения КАК ВКМ_ИдентификаторГруппыДляОповещения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл  
		
		TokenTG = Выборка.TokenTG;
		Chat_id = Выборка.Chat_id;
		ТекстСообщения =  Выборка.ТекстСообщения;
		
		АдресТг = "api.telegram.org";
		Соединение = Новый HTTPСоединение(АдресТг,443,,,,,Новый ЗащищенноеСоединениеOpenSSL());
		ДанныеКОтправке = Новый Структура;
		ДанныеКОтправке.Вставить("text", ТекстСообщения);
		ЗаписьJSON = Новый ЗаписьJSON();
		ЗаписьJSON.УстановитьСтроку(); 
		ЗаписатьJSON(ЗаписьJSON,ДанныеКОтправке);
		СтрокаJSON = ЗаписьJSON.Закрыть();
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("content-type", "application/json");
		АдресЗапроса = "bot" + TokenTG + "/sendMessage?chat_id=" + Формат(Chat_id,"ЧГ=");
		Запрос = Новый HTTPЗапрос(АдресЗапроса,Заголовки);
		Запрос.УстановитьТелоИзСтроки(СтрокаJSON);
		
		Попытка
			Результат = Соединение.ОтправитьДляОбработки(Запрос);
			
			Если Не Результат.КодСостояния = 200 Тогда
				ВызватьИсключение "Что-то пошло не так";
			КонецЕсли;
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.Удалить();
			
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"),УровеньЖурналаРегистрации.Ошибка
			,,,ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;
		
			
	КонецЦикла;
	

	
КонецПроцедуры